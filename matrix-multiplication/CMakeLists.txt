cmake_minimum_required(VERSION 3.22)
project(matrix-multiplication VERSION 0.0.1 DESCRIPTION "Nomp examples" LANGUAGES C)
option(ENABLE_OPENCL "Build OpenCL example" ON)
option(ENABLE_OPENMP "Build OpenMP example" ON)
option(ENABLE_CUDA "Build Cuda example" OFF)
option(ENABLE_ISPC "Build ISPC example" OFF)
option(ENABLE_OMP_OFFLOAD "Enable GPU offloading" OFF)

if (ENABLE_OPENCL)
  add_executable(ocl-matrix-mul opencl.c)
  find_package(OpenCL REQUIRED)
  target_link_libraries(ocl-matrix-mul PRIVATE OpenCL::OpenCL)
endif()
if (ENABLE_OPENMP)
  add_executable(omp-matrix-mul openmp.c)
  find_package(OpenMP REQUIRED)
  target_link_libraries(omp-matrix-mul PRIVATE OpenMP::OpenMP_C)
endif()
if (ENABLE_CUDA)
  enable_language(CUDA)
  add_executable(cuda-matrix-mul cuda.cu)
endif()
if (ENABLE_ISPC)
  add_custom_command(OUTPUT matrix-mul.o matrix-mul.h
                    COMMAND ispc --target=avx2 --arch=x86-64
                            ${CMAKE_SOURCE_DIR}/matrix-mul.ispc
                            -h matrix-mul.h -o matrix-mul.o
                    DEPENDS matrix-mul.ispc)
  add_library(matrix-mul STATIC matrix-mul.o)
  set_target_properties(matrix-mul PROPERTIES LINKER_LANGUAGE C)
  add_executable(ispc-matrix-mul ispc.c)
  target_link_libraries(ispc-matrix-mul matrix-mul)
  target_compile_definitions(ispc-matrix-mul PRIVATE KERNEL_FILE="${CMAKE_BINARY_DIR}/matrix-mul.h")
endif()
if (ENABLE_OMP_OFFLOAD)
  add_executable(omp-offload-matrix-mul omp_offload.c)
  find_package(OpenMP REQUIRED)
  include(../cmake/OpenMPRuntime.cmake)
  target_link_libraries(omp-offload-matrix-mul PRIVATE -lm)
endif()
