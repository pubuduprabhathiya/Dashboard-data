cmake_minimum_required(VERSION 3.22)
project(pi-integration VERSION 0.0.1 DESCRIPTION "Nomp examples - Pi integration" LANGUAGES C)

option(ENABLE_OPENCL "Build OpenCL example" ON)
option(ENABLE_OPENMP "Build OpenMP example" ON)
option(ENABLE_CUDA "Build Cuda example" OFF)
option(ENABLE_ISPC "Build ISPC example" OFF)
option(ENABLE_OMP_OFFLOAD "Enable GPU offloading" OFF)

if (ENABLE_OPENCL)
  add_executable(ocl-pi-integration opencl.c)
  find_package(OpenCL REQUIRED)
  target_link_libraries(ocl-pi-integration PRIVATE OpenCL::OpenCL)
endif()
if (ENABLE_OPENMP)
  add_executable(omp-pi-integration openmp.c)
  find_package(OpenMP REQUIRED)
  target_link_libraries(omp-pi-integration PRIVATE OpenMP::OpenMP_C)
endif()
if (ENABLE_CUDA)
  enable_language(CUDA)
  add_executable(cuda-pi-integration cuda.cu)
endif()
if (ENABLE_ISPC)
  add_custom_command(OUTPUT pi-integration.o pi-integration.h
          COMMAND ispc --target=avx2 --arch=x86-64
          ${CMAKE_SOURCE_DIR}/pi-integration.ispc
          -h pi-integration.h -o pi-integration.o
          DEPENDS pi-integration.ispc)
  add_library(pi-integration STATIC pi-integration.o)
  set_target_properties(pi-integration PROPERTIES LINKER_LANGUAGE C)
  add_executable(ispc-pi-integration ispc.c)
  target_link_libraries(ispc-pi-integration pi-integration)
  target_compile_definitions(ispc-pi-integration PRIVATE KERNEL_FILE="${CMAKE_BINARY_DIR}/pi-integration.h")
endif()
if (ENABLE_OMP_OFFLOAD)
  add_executable(omp-offload-pi-integration omp_offload.c)
  find_package(OpenMP REQUIRED)
  include(../cmake/OpenMPRuntime.cmake)
endif()
